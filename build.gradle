plugins {
    id "org.springframework.boot" version "2.2.0.RELEASE" apply false
    id "io.spring.dependency-management" version "1.0.8.RELEASE" apply false
    id "com.gorylenko.gradle-git-properties" version "2.0.0" apply false
}

configure(subprojects) { project ->
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: "com.gorylenko.gradle-git-properties"
    apply from: '../libraries.gradle'

    group 'ru.romanow.services'

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        compile "javax.validation:validation-api:${validationApiVersion}"
        compile "org.apache.commons:commons-lang3:${commonsLangVersion}"
        compile "com.google.code.gson:gson:${gsonVersion}"
        compile "com.google.guava:guava:${guavaVersion}"
        compile "com.google.code.findbugs:jsr305:${findBugsVersion}"
        compile "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        
        if (project.name.endsWith('-service')) {
            compile "ru.romanow.core:commons:${utilsVersion}"
            compile "ru.romanow.core:spring-rest-client:${utilsVersion}"
            
            compile "org.springframework.boot:spring-boot-starter-web"
            compile "org.springframework.boot:spring-boot-starter-data-jpa"
            compile "org.springframework.boot:spring-boot-starter-actuator"
            compile "org.springframework.boot:spring-boot-starter-security"
            compile "org.springframework.boot:spring-boot-starter-logging"

            compile "org.springframework.cloud:spring-cloud-netflix-eureka-client"
            compile "org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j"
            compile "org.springframework.cloud:spring-cloud-loadbalancer"
            compile "org.springframework.cloud:spring-cloud-starter-zipkin"

            compile "io.micrometer:micrometer-registry-prometheus"
            
            compile "com.netflix.eureka:eureka-client"
            compile "com.netflix.ribbon:ribbon-eureka"

            compile "ch.qos.logback:logback-classic"
            compile "net.logstash.logback:logstash-logback-encoder:${logstahVersion}"
            
            compile "org.postgresql:postgresql"
            compile "org.flywaydb:flyway-core"

            compile "io.swagger:swagger-annotations:${swaggerVersion}"
            compile "io.springfox:springfox-swagger2:${swaggerSpringVersion}"
        }
    }
}