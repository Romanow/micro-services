buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "com.github.ksoichiro:gradle-build-info-plugin:${gitPluginVersion}"
    }
}

configure(subprojects) { project ->
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply from: '../libraries.gradle'

    group 'ru.romanow.services'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        compile "javax.validation:validation-api:${validationApiVersion}"
        compile "org.apache.commons:commons-lang3:${commonsLangVersion}"
        compile "com.google.code.gson:gson:${gsonVersion}"
        compile "com.google.guava:guava:${guavaVersion}"
        compile "com.google.code.findbugs:jsr305:${findBugsVersion}"
        compile "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        
        if (project.name.endsWith('-service')) {
            compile "org.springframework.boot:spring-boot-starter-web"
            compile "org.springframework.boot:spring-boot-starter-data-jpa"
            compile "org.springframework.boot:spring-boot-starter-actuator"
            compile "org.springframework.boot:spring-boot-starter-logging"

            compile "org.springframework.cloud:spring-cloud-netflix-eureka-client"
            compile "org.springframework.cloud:spring-cloud-starter-netflix-ribbon"
            compile "org.springframework.cloud:spring-cloud-starter-netflix-hystrix"

            compile "com.netflix.eureka:eureka-client"
            compile "com.netflix.ribbon:ribbon-eureka"

            compile "org.postgresql:postgresql"
            compile "org.flywaydb:flyway-core"

            compile "io.swagger:swagger-annotations:${swaggerVersion}"
            compile "io.springfox:springfox-swagger2:${swaggerSpringVersion}"
        }
    }
}